# Wirth syntax notation
Wirth语法符号

* https://en.wikipedia.org/wiki/Wirth_syntax_notation

**Wirth syntax notation(WSN)** is a metasyntax, that is, a formal way to describe formal languages. Originally proposed by Niklaus Wirth in 1977 as an alternative to Backus–Naur form(BNF). It has several advantages over BNF in that it contains an explicit iteration construct, and it avoids the use of an explicit symbol for the empty string (such as `<empty>` or `ε`).
Wirth语法符号（WSN）是一种元语法，即描述形式语言的形式方式。最初由 Niklaus Wirth 于 1977 年提出，作为巴科斯-诺尔形式 (BNF) 的替代方案。与 BNF 相比，它有几个优点，因为它包含显式迭代构造，并且避免对空字符串使用显式符号（例如 或 ε）。

WSN has been used in several international standards, starting with ISO 10303-21 It was also used to define the syntax of EXPRESS, the data modelling language of STEP.
从 ISO 10303-21 开始，WSN 已在多个国际标准中使用。它还用于定义 EXPRESS（STEP 的数据建模语言）的语法。

## WSN defined in itself
WSN 本身定义

```
 SYNTAX     = { PRODUCTION } .
 PRODUCTION = IDENTIFIER "=" EXPRESSION "." .
 EXPRESSION = TERM { "|" TERM } .
 TERM       = FACTOR { FACTOR } .
 FACTOR     = IDENTIFIER
            | LITERAL
            | "[" EXPRESSION "]"
            | "(" EXPRESSION ")"
            | "{" EXPRESSION "}" .
 IDENTIFIER = letter { letter } .
 LITERAL    = """" character { character } """" .
```

The **equals** sign indicates a production.
The element on the left is defined to be the combination of elements on the right.
A production is terminated by a full stop (period).
等号表示产生式。
左边的元素被定义为右边元素的组合。
生产以句号（句号）结束。

- Repetition is denoted by curly brackets, e.g., `{a}` stands for `ε | a | aa | aaa | ...`.
  重复用大括号表示

- Optionality is expressed by square brackets, e.g., `[a]b` stands for `ab | b`.
  可选性用方括号表示

- Parentheses serve for groupings, e.g., `(a|b)c` stands for `ac | bc`.
  括号用于分组

We take these concepts for granted today, but they were novel and even controversial in 1977. Wirth later incorporated some of the concepts (with a different syntax and notation) into **extended Backus–Naur form**.
今天我们认为这些概念是理所当然的，但它们在 1977 年是新颖的，甚至是有争议的。沃斯后来将其中一些概念（具有不同的语法和符号）纳入扩展的巴科斯-诺尔形式。

Notice that `letter` and `character` are left undefined. This is because numeric characters (digits 0 to 9) may be included in both definitions or excluded from one, depending on the language being defined, e.g.:
请注意， `letter` 和 `character` 未定义。这是因为数字字符（数字 0 到 9）可能包含在两个定义中或从一个定义中排除，具体取决于所定义的语言，例如：

```
 digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" .
 upper-case = "A" | "B" | … | "Y" | "Z" .
 lower-case = "a" | "b" | … | "y" | "z" .
 letter = upper-case | lower-case .
```

If `character` goes on to include `digit` and other printable ASCII characters, then it diverges even more from `letter`, which one can assume does not include the digit characters or any of the special (non-alphanumeric) characters.
如果 `character` 继续包含 `digit` 和其他可打印的 ASCII 字符，那么它与 `letter` 的差异更大，可以假设它不包含数字字符或任何特殊（非字母数字）字符。

## Another example
另一个例子

The syntax of BNF can be represented with WSN as follows, based on translating the BNF example of itself:
基于翻译自身的 BNF 示例，BNF 的语法可以用 WSN 表示如下：

```
 syntax         = rule [ syntax ] .
 rule           = opt-whitespace "<" rule-name ">" opt-whitespace "::="
                  opt-whitespace expression line-end .
 opt-whitespace = { " " } .
 expression     = list [ "|" expression ] .
 line-end       = opt-whitespace EOL | line-end line-end .
 list           = term [ opt-whitespace list ] .
 term           = literal | "<" rule-name ">" .
 literal        = """" text """" | "'" text "'" .
```

This definition appears overcomplicated because the concept of "optional whitespace" must be explicitly defined in BNF, but it is implicit in WSN. Even in this example, `text` is left undefined, but it is assumed to mean "`ASCII-character { ASCII-character }`". (`EOL` is also left undefined.) Notice how the kludge `"<" rule-name ">"` has been used twice because `text` was not explicitly defined.
这个定义显得过于复杂，因为“可选空白”的概念在 BNF 中必须明确定义，但在 WSN 中却是隐含的。即使在此示例中， `text` 也未定义，但假定其含义为“ `ASCII-character { ASCII-character }` ”。（ `EOL` 也未定义。）请注意，由于 `text` 未明确定义，所以拼凑 `"<" rule-name ">"` 已被使用两次。

One of the problems with BNF which this example illustrates is that by allowing both single-quote and double-quote characters to be used for a `literal`, there is an added potential for human error in attempting to create a machine-readable syntax. One of the concepts that migrated to later meta syntaxes was the idea that giving the user multiple choices made it harder to write parsers for grammars defined by the syntax, so computer languages in general have become more restrictive in how a *quoted-literal* is defined.
此示例说明的 BNF 问题之一是，通过允许对 `literal` 使用单引号和双引号字符，在尝试创建机器时可能会出现人为错误- 可读语法。迁移到后来的元语法的概念之一是，为用户提供多种选择使得为语法定义的语法编写解析器变得更加困难，因此计算机语言通常在如何定义引用文字方面变得更加严格。
